apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

version = '1.0'

if (!project.hasProperty("pdffile")) {
    ext.pdffile = "https://www.heart.org/idc/groups/heart-public/@wcm/@hcm/documents/downloadable/ucm_300310.pdf"
}

repositories {
    mavenCentral()
    maven {
      url "http://mvnrepository.com"
    }
}

dependencies {
    compile 'org.apache.tika:tika-parsers:1.4'
    compile 'net.sf.jwordnet:jwnl:1.3.3'
    compile 'org.apache.opennlp:opennlp-tools:1.5.3'
}

task fatJar(type: Jar) {
	manifest {
        attributes 'Implementation-Title': 'Parse a pdf',  
        	'Implementation-Version': version,
        	'Main-Class': 'rsdets1.PdfParse'
    }
    baseName = 'rsdets1-tika-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA' 
    with jar
}

task runPyNlp(type:Exec) {
   workingDir './scripts'
   commandLine 'python', 'processFile.py'
} 

task runGoogleNlp(type:Exec) {
   workingDir '../models/syntaxnet'
   commandLine 'sh', '../../rsdets1/scripts/processFileGoogle.sh'
} 

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

configurations {
    scheduleRuntime
 }

dependencies {
    scheduleRuntime files("build/libs/rsdets1-tika-all-${version}.jar")
}

task runPdfParserJar(type: JavaExec) {
    main = "rsdets1.PdfParse"
    workingDir = "./"
    classpath = configurations.scheduleRuntime
    args("${pdffile}")
  }

